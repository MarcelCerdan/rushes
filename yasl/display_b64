#!yasl

{
	=:1 // number
	1:2 // incrementer
	1
	@ (
		2:= 2:= *
		=
		1:= ==
		? (2:= 0) : (
			1:= >
			? (-1 0) : (
				1:2
				2:+:2
				1
			)
		)
	)
	2:#!
	1:#!
}sqrt

{
	=:1
	"A" >=
	1:= "Z" <=
	&&
	? ( 1:= "A" -:2 ) : (
		1:= "a" >=
		1:= "z" <=
		&&
		? ( 1:= "a" - 26 +:2 ) : (
			1:= "0" >=
			1:= "9" <=
			&&
			? ( 1:= "0" - 52 +:2 ) : (
				1:= "+" ==
				? 62:2 : (
					1:= "/" ==
					? 63:2 : ("Map error\n" print 0 exit)
				)
			)
		)
	)
	1:!
}convert

{
	""
	read
	@ (
		+
		read
	)
	+
	][
}read_file

{
	"\e[38;2;":4 4:print
	4:print // Red value
	";":4 4:print
	4:print // Green value
	";":4 4:print
	4:print // Blue value
	"m":4 4:print
	"██":4 4:print // Square 
}print_color

#!
read_file
#
4 /
"Image received, nb pixels : " print
= print
"\n" print
sqrt
"Found square : " print
= print
"\n" print
=:9 // line size
!!
9:=
-1 ==
? ( "Not square\n" print 0 exit )
][
!
@ (
	convert
	convert
	2:2
	2:!^:2
	convert
	3:2
	2:!^:2
	convert
	4:2
	2:!^:2

	// Blue
	2:=:4
	2:!
	2:=:4
	3:4
	4:&:4
	6:4
	4:<<:4
	4:+:4

	// Green
	2:=:4
	2:4
	4:>>:4
	2:!
	2:=:4
	15:4
	4:&:4
	4:4
	4:<<:4
	4:+:4

	// Red
	2:=:4 
	2:!
	4:4
	4:>>:4
	2:=:4
	2:4
	4:<<:4
	4:+:4
	#
)

4:(# 3 /):4
9:=:4
4:/:4
4:#(
	9:=:4
	#( print_color )
	"\n":4 4:print
)

